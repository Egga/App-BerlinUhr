#!/usr/bin/perl -w
# -*- perl -*-

#
# Author: Slaven Rezic
#
# Copyright (C) 2010 Slaven Rezic. All rights reserved.
# This program is free software; you can redistribute it and/or
# modify it under the same terms as Perl itself.
#
# Mail: slaven@rezic.de
# WWW:  http://www.rezic.de/eserte/
#

use strict;
use DateTime;
use Tk;

my $mw = tkinit;
my $c = $mw->Canvas(-width => 400, -height => 200)->pack;

sub redraw {
    my $now = DateTime->now(time_zone => "local");
    my $hour_top = $now->hour / 5;
    my $hour_bottom = $now->hour % 5;
    my $min_top = $now->minute / 5;
    my $min_bottom = $now->minute % 5;
    my $sec = $now->second;

    $c->delete("all");
    $c->createOval(180,1,220,40, -fill => $sec % 2 == 0 ? 'orange' : $c->cget(-bg));
    my $y = 40;
    for (1 .. 4)    { $c->createRectangle(($_-1)*100, $y, $_*100, $y+40, -fill => ($_ <= $hour_top ? 'red' : undef)) }
    $y+=40;
    for (1 .. 4) { $c->createRectangle(($_-1)*100, $y, $_*100, $y+40, -fill => ($_ <= $hour_bottom ? 'red' : undef)) }
    $y+=40;
    for (1 .. 11)     { $c->createRectangle(($_-1)*(400/11), $y, $_*(400/11), $y+40, -fill => ($_ > $min_top ? undef : $_%3==0 ? 'red' : 'orange')) }
    $y+=40;
    for (1 .. 4)  { $c->createRectangle(($_-1)*100, $y, $_*100, $y+40, -fill => ($_ <= $min_bottom ? 'orange' : undef)) }
}

redraw();
$c->repeat(1000, \&redraw);

MainLoop;

__END__
